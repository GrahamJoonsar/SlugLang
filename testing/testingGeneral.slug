string m0 "####################"
string m1 "#  # #             #"
string m2 "#  # #         #   #"
string m3 "#  # ######### ### #"
string m4 "#                # #"
string m5 "#  ###### ######## #"
string m6 "#  #             #$#"
string m7 "####################"

# Player positions
int px 1 
int py 1

bool running true

func move ()
    readStr input
    if input == "w"
        decr py
    elseif input == "s"
        incr py
    elseif input == "a"
        decr px
    elseif input == "d"
        incr px
    elseif input == "q"
        setm running false # Setting and mutating running to false

    # Getting the position where the player wants to move
    exec $ "getch m" py " " px " testCh" $

    if testCh == " " # Since the variables only change globally if mutated
        mutate px
        mutate py
    elseif testCh == "$" # You won
        system "cls"
        println "You Win!"
        quit
end

while running
    for int i 0 ; i < 8 ; incr i
        string line "" # The current line
        for int j 0 ; j < 20 ; incr j # looping through columns
            # Getting the current character of the board
            exec $ "getch m" i " " j " CurrentChar" $
            if py == i and px == j # If player at position
                concat line "O" # Put the xharacter on the line
            else
                #Adding it to the current line
                concat line CurrentChar
        endw
        println line 
    endw
    move
    system "cls"
endw
